apply plugin: 'java'

group 'pt.lsts'
version '5.4.26.1'

ext
{
   imcDir = project.hasProperty('imcDir') ? project.getProperty('imcDir') : rootProject.file("../imc").absolutePath
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.5'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.2'
    implementation group: 'com.google.code.gson', name:'gson', version: '2.5'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '2.5.8'

}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/generated/java', 'src/sender/java']
        }

        resources {
            srcDirs = ['src/main/resources', 'src/generated/resources', 'src/sender/resources']
        }
    }
}

//******************************************************************************
//* Generator.                                                                 *
//******************************************************************************

task generate(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "pt.lsts.imc.generator.ClassGenerator"
    args rootProject.ext.imcDir, rootProject.file('src/generated').absolutePath
}

task protocol(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "pt.lsts.imc.net.IMCProtocol"    
}

task sender(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "pt.lsts.imc.sender.MessageSender"
}

//******************************************************************************
//* Publishing.                                                                *
//******************************************************************************

apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

publishing {
    repositories {
        maven {
            url "file://" + System.getProperty("user.home") + "/maven"
        }
    }
}

task printVersion {
   doLast { 
       logger.lifecycle(project.version)
   }
}